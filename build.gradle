plugins {
	id 'java'
	id 'com.diffplug.spotless' version '6.25.0'
	id 'jacoco'
}

group = 'com.machinalny'
version = '0.0.1-SNAPSHOT'

repositories {
	mavenCentral()
}

dependencies {
	testImplementation platform('org.junit:junit-bom:5.10.0')
	testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.required = true
		html.required = true
	}
}

spotless {
	// optional: limit format enforcement to just the files changed by this feature branch
//	ratchetFrom 'origin/main'

	format 'misc', {
		// define the files to apply `misc` to
		target '*.gradle', '*.md', '.gitignore'

		// define the steps to apply to those files
		trimTrailingWhitespace()
		indentWithTabs() // or spaces. Takes an integer argument if you don't like 4
		endWithNewline()
	}
	java {
		// don't need to set target, it is inferred from java
		// apply a specific flavor of google-java-format
		googleJavaFormat()
		// fix formatting of type annotations
		formatAnnotations()
		// make sure every file has the following copyright header.
		// optionally, Spotless can set copyright years by digging
		// through git history (see "license" section below)
		licenseHeader '/* Lukasz Lopusinski (machinalny) Â©$YEAR  */'
	}
}
